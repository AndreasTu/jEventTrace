plugins {
    id 'base'
    id 'jacoco'
    id 'com.github.spotbugs' version '5.0.14' apply false
    id 'com.github.ben-manes.versions' version '0.47.0'
}

version = "1.0.3-SNAPSHOT"

subprojects {
    project.apply plugin: "java"
    project.apply plugin: "groovy"
    project.apply plugin: "publishing"
    project.apply plugin: "com.github.spotbugs"

    group = "com.github.AndreasTu.jEventTrace"
    version = rootProject.version

    tasks.withType(JavaCompile).configureEach {
        options.release.set(17)
        options.encoding = "UTF-8"
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    tasks.named("jar", Jar).configure {
        it.setPreserveFileTimestamps(false)
        it.setReproducibleFileOrder(true)
        manifest {
            attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "Andreas Turban",
            )
        }
    }

    tasks.named("processResources", ProcessResources).configure {
        from(rootProject.file("LICENSE")) {
            into("META-INF")
        }
    }

    project.dependencies {
        compileOnly libs.googleFindbugsAnno
        compileOnly libs.spotBugsAnno

        testImplementation libs.groovy
        testImplementation(libs.spockCore) {
            exclude module: "groovy"
        }
        testRuntimeOnly libs.junitPlatform
    }

    tasks.named("spotbugsTest").configure {
        //We do not execute spotbugs on test code
        enabled = false
    }
}

apply from: "gradle/jacoco.gradle"
