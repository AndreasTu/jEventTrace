plugins {
    id 'base'
    id 'com.github.spotbugs' version '5.0.14' apply false

}

version = "1.0.2"

subprojects {
    project.apply plugin: "java"
    project.apply plugin: "groovy"
    project.apply plugin: "publishing"
    project.apply plugin: "com.github.spotbugs"

    group = "com.github.AndreasTu"
    version = rootProject.version

    tasks.withType(JavaCompile).configureEach {
        options.release.set(17)
        options.encoding = "UTF-8"
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    tasks.named("jar", Jar).configure {
        it.setPreserveFileTimestamps(false)
        it.setReproducibleFileOrder(true)
        manifest{
            attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "Andreas Turban",
            )
        }
    }

    tasks.named("processResources", ProcessResources).configure {
        from(rootProject.file("LICENSE")){
            into("META-INF")
        }
    }

    project.dependencies {
        compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
        compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.7.3'

        testImplementation group: 'org.apache.groovy', name: 'groovy', version: '4.0.12'
        testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-4.0'
        testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.9.3'
    }

    tasks.named("spotbugsTest").configure {
        //We do not execute spotbugs on test code
        enabled = false
    }
}
